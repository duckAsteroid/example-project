plugins {
  id 'application'
  id 'com.palantir.git-version' version '3.1.0'
 }

group = 'com.github.duckasteroid'

dependencies {
  implementation project(':api')
  runtimeOnly project(':emitter')
  runtimeOnly project(':receiver')
}

def getOurVersion() {
  def details = versionDetails()
  def version = details.lastTag.substring(1) // get rid of leading 'v'
  def suffix = "" // default no suffix
  if (details.branchName != null && details.branchName.equals("develop")) {
    // if we are on develop branch: auto increment last version segment
    def lastSplit = version.lastIndexOf('.')
    if (lastSplit > 0) {
      def prefix = version.substring(0, lastSplit)
      def lastElement = version.substring(lastSplit + 1)
      if (lastElement.isInteger()) {
        def num = lastElement as Integer
        version = prefix + "." + (num++)
      }
    }
  }
  if (!details.isCleanTag) {
    suffix = "-SNAPSHOT"
  }
  return version + suffix
}

version = getOurVersion()

allprojects {
  apply plugin: 'java'
  apply plugin:  'maven-publish'

  repositories {
    mavenCentral()
  }

  dependencies {
    // Other dependencies...

    // JUnit Jupiter API and test implementation
    testImplementation libs.junit.api

    // JUnit Jupiter Engine
    testRuntimeOnly libs.junit.engine
  }


  test {
    useJUnitPlatform()
  }
}


application {
  mainClass = 'com.github.duckasteroid.Main'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = project.name
        //description = "Project ${getProject().path}"
        url = "http://duckasteroid.github.io/${rootProject.name}/"
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          connection = "scm:git:git://github.com/duckAsteroid/${rootProject.name}.git"
          developerConnection = "scm:git:ssh://github.com:duckAsteroid/${rootProject.name}.git"
          url ="https://github.com/duckAsteroid/${rootProject.name}/tree/master"
        }
        developers {
          developer {
            id = 'duckAsteroid'
            name = 'Chris Senior'
            email ='christopher.senior@gmail.com'
          }
        }
      }
    }
  }
  // add GitHub packages repo
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/cognitranlimited/products-gradle-plugins"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
    maven {
      name = 'LocalTest'
      // Change the URL to your desired local directory
      url = uri("file://${projectDir}/build/repo")
    }
  }
}

